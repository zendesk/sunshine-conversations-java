buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.+'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    id 'signing'
    id 'org.openapi.generator' version '7.15.0'
}

group = 'com.zendesk'
version = '17.1.0'

def calculateSnapshotVersion() {
    def sha = "git rev-parse --short HEAD".execute(null, project.rootDir).text.trim()
    return "${project.version}-${sha}-SNAPSHOT"
}

ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''
ext["sonatypeStagingProfileId"] = ''
ext["snapshotVersion"] = calculateSnapshotVersion()

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    ext["signing.keyId"] = System.getenv('MAVEN_SIGNING_KEY_ID')
    ext["signing.password"] = System.getenv('MAVEN_SIGNING_PASSWORD')
    ext["signing.secretKeyRingFile"] = System.getenv('MAVEN_SIGNING_SECRET_KEY_RING_FILE')
    ext["ossrhUsername"] = System.getenv('MAVEN_OSSRH_USERNAME')
    ext["ossrhPassword"] = System.getenv('MAVEN_OSSRH_PASSWORD')
    ext["sonatypeStagingProfileId"] = System.getenv('SONATYPE_STAGING_PROFILE_ID')
}

repositories {
    mavenCentral()
}

java {
    withSourcesJar()
    withJavadocJar()
}

task copyDeps(type: Copy) {
  from configurations.runtimeClasspath
  into 'build/lib'
}

if(hasProperty('target') && target == 'android') {

    apply plugin: 'com.android.library'
    apply plugin: 'com.github.dcendents.android-maven'

    android {
        compileSdkVersion 23
        buildToolsVersion '23.0.2'
        defaultConfig {
            minSdkVersion 14
            targetSdkVersion 23
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_17
            targetCompatibility JavaVersion.VERSION_17
        }

        // Rename the aar correctly
        libraryVariants.all { variant ->
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.aar')) {
                    def fileName = "${project.name}-${variant.baseName}-${version}.aar"
                    output.outputFile = new File(outputFile.parent, fileName)
                }
            }
        }

        dependencies {
            provided 'jakarta.annotation-api:2.1.1'
            provided 'jakarta.ws.rs-api:3.1.0'
        }
    }

    afterEvaluate {
        android.libraryVariants.all { variant ->
            def task = project.tasks.create "jar${variant.name.capitalize()}", Jar
            task.description = "Create jar artifact for ${variant.name}"
            task.dependsOn variant.javaCompile
            task.from variant.javaCompile.destinationDir
            task.destinationDir = project.file("${project.buildDir}/outputs/jar")
            task.archiveName = "${project.name}-${variant.baseName}-${version}.jar"
            artifacts.add('archives', task);
        }
    }

    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier = 'sources'
    }

    artifacts {
        archives sourcesJar
    }

} else {

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    // Support Java 17+ for broad compatibility while allowing modern features
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17


    task execute(type:JavaExec) {
       mainClass = System.getProperty('mainClass')
       classpath = sourceSets.main.runtimeClasspath
    }
}

ext {
    swagger_annotations_version = "2.2.37"
    jackson_version = "2.20.0"
    jackson_databind_version = "2.20.0"
    jackson_annotations_version = "2.20"
    jackson_databind_nullable_version = "0.2.7"
    threetenbp_version = "2.18.2"
    resteasy_version = "6.2.12.Final"
    junit_version = "5.10.2"
    jakarta_annotation_version = "2.1.1"
}

dependencies {
    implementation "io.swagger.core.v3:swagger-annotations:$swagger_annotations_version"
    implementation "jakarta.annotation:jakarta.annotation-api:$jakarta_annotation_version"
    implementation "jakarta.ws.rs:jakarta.ws.rs-api:3.1.0"
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    implementation "org.jboss.resteasy:resteasy-client:$resteasy_version"
    implementation "org.jboss.resteasy:resteasy-multipart-provider:$resteasy_version"
    implementation "org.jboss.resteasy:resteasy-jackson2-provider:$resteasy_version"
    implementation "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jackson_annotations_version"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_databind_version"
    implementation "com.github.joschi.jackson:jackson-datatype-threetenbp:$threetenbp_version"
    implementation "org.openapitools:jackson-databind-nullable:$jackson_databind_nullable_version"
    testImplementation "org.junit.jupiter:junit-jupiter:$junit_version"
}

// OpenAPI Generator configuration
openApiGenerate {
    generatorName = "java"
    inputSpec = "$rootDir/api/openapi.yaml"
    outputDir = "$rootDir"
    templateDir = "$rootDir/custom-templates"
    apiPackage = "com.zendesk.sunshine_conversations_client.api"
    modelPackage = "com.zendesk.sunshine_conversations_client.model"
    invokerPackage = "com.zendesk.sunshine_conversations_client"
    validateSpec = true

    configOptions = [
        "library": "resteasy",                  // Use RESTEasy HTTP client
        "dateLibrary": "java8",                 // Use Java 8 time API
        "useJakartaEe": "true",                 // Use Jakarta EE APIs
        "serializationLibrary": "jackson",      // Use Jackson for JSON
        "openApiNullable": "false",             // Don't use OpenAPI nullable wrapper
        "legacyDiscriminatorBehavior": "false", // Use new discriminator behavior for better interface compatibility
        "enumUnknownDefaultCase": "false",      // Don't generate unknown enum case
        "hideGenerationTimestamp": "true",      // Hide generation timestamp for cleaner diffs
        "groupId": "com.zendesk",
        "artifactId": "sunshine-conversations-client",
        "artifactVersion": project.version.toString()
    ]
}

// Task to fix discriminator values after generation
task fixDiscriminators {
    description = "Fix @JsonTypeName values to match discriminator mappings"

    doLast {
        // Fix Content subtype discriminator values
        def discriminatorFixes = [
            'TextMessage.java': ['@JsonTypeName("textMessage")', '@JsonTypeName("text")'],
            'CarouselMessage.java': ['@JsonTypeName("carouselMessage")', '@JsonTypeName("carousel")'],
            'FileMessage.java': ['@JsonTypeName("fileMessage")', '@JsonTypeName("file")'],
            'FormMessage.java': ['@JsonTypeName("formMessage")', '@JsonTypeName("form")'],
            'FormResponseMessage.java': ['@JsonTypeName("formResponseMessage")', '@JsonTypeName("formResponse")'],
            'ImageMessage.java': ['@JsonTypeName("imageMessage")', '@JsonTypeName("image")'],
            'ListMessage.java': ['@JsonTypeName("listMessage")', '@JsonTypeName("list")'],
            'LocationMessage.java': ['@JsonTypeName("locationMessage")', '@JsonTypeName("location")'],
            'TemplateMessage.java': ['@JsonTypeName("templateMessage")', '@JsonTypeName("template")']
        ]

        discriminatorFixes.each { fileName, fix ->
            def file = file("src/main/java/com/zendesk/sunshine_conversations_client/model/${fileName}")
            if (file.exists()) {
                def content = file.text
                def newContent = content.replace(fix[0], fix[1])
                if (content != newContent) {
                    file.text = newContent
                    println "Fixed discriminator in ${fileName}"
                }
            }
        }
    }
}

// Make sure fixDiscriminators runs after openApiGenerate
tasks.openApiGenerate.finalizedBy tasks.fixDiscriminators

// Make compilation and jar depend on generation
compileJava.dependsOn tasks.openApiGenerate
sourcesJar.dependsOn tasks.openApiGenerate

// Use only src/main/java as the source directory
sourceSets {
    main {
        java {
            srcDirs = ["src/main/java"]
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom.withXml {
                asNode().appendNode('name', 'sunshine-conversations-client')
                asNode().appendNode('description', 'Sunshine Conversations Client')
                asNode().appendNode('url', 'https://zendesk.com')
                asNode().appendNode('scm').
                    appendNode('connection', 'https://github.com/zendesk/sunshine-conversations-java').parent().
                    appendNode('developerConnection', 'https://github.com/zendesk/sunshine-conversations-java').parent().
                    appendNode('url', 'https://github.com/zendesk/sunshine-conversations-java')
                asNode().appendNode('licenses').appendNode('license').
                        appendNode('name', 'Apache-2.0').parent().
                        appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.html').parent().
                        appendNode('distribution', 'repo')
                asNode().appendNode('developers').appendNode('developer').
                        appendNode('name', 'Sunshine Conversations Engine').parent().
                        appendNode('email', 'sunco-eng-conversation-engine@zendesk.com').parent().
                        appendNode('organization', 'Sunshine Conversations Zendesk').parent().
                        appendNode('organizationUrl', 'smooch.io')
              }
        }

        zdArtifactory(MavenPublication) {
          from components.java
          groupId = 'com.zendesk'
          artifactId = 'sunshine-conversations-client'
          version = snapshotVersion
        }

        zdArtifactoryRelease(MavenPublication) {
          from components.java
          groupId = 'com.zendesk'
          artifactId = 'sunshine-conversations-client'
          version = project.version  // Release version without snapshot suffix
        }
    }
    repositories {
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"

            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }

        maven {
          name = "zendeskArtifactory"
          url = "https://zdrepo.jfrog.io/zdrepo/libs-snapshots-local"
          credentials {
              username System.env.ARTIFACTORY_USERNAME
              password System.env.ARTIFACTORY_API_KEY
          }
        }

        maven {
          name = "zendeskArtifactoryRelease"
          url = "https://zdrepo.jfrog.io/zdrepo/libs-releases-local"
          credentials {
              username System.env.ARTIFACTORY_USERNAME
              password System.env.ARTIFACTORY_API_KEY
          }
        }
    }
}

signing {
    sign publishing.publications
}

tasks.withType(Sign) {
    onlyIf { project.getProperties().get("disableSigning") != "true" }
}

task publishSnapshotToZendeskArtifactory {
    dependsOn tasks.named('publishZdArtifactoryPublicationToZendeskArtifactoryRepository')
    description = 'Publish snapshot to the Zendesk artifactory'

    doLast {
        println("Published snapshot version: " + snapshotVersion)
    }
}

task publishReleaseToZendeskArtifactory {
    dependsOn tasks.named('publishZdArtifactoryReleasePublicationToZendeskArtifactoryReleaseRepository')
    description = 'Publish release to the Zendesk artifactory'

    doLast {
        println("Published release version: " + project.version)
    }
}
